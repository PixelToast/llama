~\iSucc(\n
    n (\a\b\sgn sgn (uSucc a) b)
)

~\iPred(\n
    n \a\b \sgn sgn a~uSucc b
)

~\iAdd(\m\n
    m \ma\mb n \na\nb \sgn sgn (uAdd ma na)~uAdd mb nb
)

~\iNeg(\n
    n \a\b \sgn sgn b a
)

~\iNormalize(\n
    n \a\b \sgn sgn (uSub a b) (uSub b a)
)

~\iSub(\m\n
    iAdd m~iNeg n
)

~\iMul(\m\n
    m \ma\mb n \na\nb uIsZero ma (
        \sgn sgn (uMul mb nb)~uMul mb na
    ) \sgn sgn (uMul ma na)~uMul ma nb
)

~\iIsZero(\n
    iNormalize n \a\b bAnd (uIsZero a)~uIsZero b
)

~\iIsNeg(\n
    n \a\b uGT b a
)

~\iIsPos(\n
    n \a\b uGT a b
)

~\iGT(\m\n
    iIsNeg~iSub n m
)

~\iLT(\m\n
    iIsNeg~iSub m n
)

~\iGE(\m\n
    bNot~iLT m n
)

~\iLE(\m\n
    bNot~iGT m n
)

~\iEQ(\m\n
    iNormalize m \ma\mb iNormalize n \na\nb
        bAnd (uEQ ma na)~uEQ mb nb
)

~\iNEQ(\m\n
    bNot~iEQ m n
)

~\iUnsigned(\n
    iNormalize n \a\b a
)
