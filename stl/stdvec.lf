//

~\vMap(\s\fn
    \f\l s (\nm\nx f (fn nm) nx) l
)

~\vFold(\v\iv\fn
    v (\e\nx\st
        nx~fn st e
    ) (\st st) iv
)

~\vFoldMap(\v\iv\fn
    \f\l v (\e\nx\st
        fn st e~\nst\ne
            f ne~nx nst
    ) (\st l) iv
)

~\vConcat(\a\b
    \f\l a f ~b f l
)

~\vExpand(\v\l
    vFold v [] \st\e
        vConcat st (l e)
)

~\vInsertEnd(\v\e
    \f\l v f~f e l
)

~\vInsertStart(\v\e
    \f\l f e~v f l
)

~\vIndexify(\v
    vFoldMap v 0 \st\e tPair (iSucc st)~tPair st e
)

~\vReverse(\v
    v (\e\nx vInsertEnd nx e) []
)

~\vLength(\v
    \f\x v (\e\nx f nx) x
)

~\vFirstWhere(\v\l\d
    v (\e\nx
        l e e nx
    ) d
)

~\vTake(\v\i
    v (\e\nx
        \idx iIsZero idx []~vInsertStart (nx~iPred idx) e
    ) (\idx []) i
)

~\vSkip(\v\i
    v (\e\nx
        \idx iIsZero idx (vInsertStart (nx~iPred idx) e)~nx~iPred idx
    ) (\idx []) i
)

~\vGet(\v\i\d
    v (\e\nx
        \idx iIsZero idx e~nx~iPred idx
    ) (\idx d) i
)

~\vWhere(\v\l
    v (\e\nx
        l e (vInsertStart nx e) nx
    ) []
)

~\vSet(\v\i\d
    vMap (vIndexify v) \e
        iIsEQ (e tFirst) i d (e tSecond)
)

~\vGenerate(\j\e
    \f\l j (\nx
        f e nx
    ) l
)

~\vGenerateRange(\i\j
    \f\l (iSub j i) (\nx\st
        f (iAdd st i)~nx (iSucc st)
    ) (\st l) 0
)

~\vIsEmpty(\v
    v (\e\nx false) true
)

~\vRemoveLast(\v
    vTake v (iPred~vLength v)
)

~\vRemoveFirst(\v
    vSkip v 1
)

~\vSlice(\v\start\end
    vTake (vSkip v start)~iSub end start
)

~\vLast(\v\d
    vIsEmpty v d~vGet v (iPred~vLength v) d
)

~\vFirst(\v\d
    v (\e\nx e) d
)

~\vIsEQ(\cmp\a\b
    bAnd (iIsEQ (vLength a)~vLength b)~vFold a <true b> (\st\e
        <(bAnd (st tFirst)~cmp e~vFirst (st tSecond) 0) ~vSkip (st tSecond) 1>
    ) tFirst
)

~\vTakeWhile(\v\lm
    \f\l v (\e\nx
        lm e (f e nx)~l
    ) l
)

~\vSkipWhile(\v\lm
    \f\l v (\e\nx\cn
        bOr cn (bNot~lm e) (f e (nx true))~nx false
    ) (\cn l) false
)

~\vInsertAt(\v\i\e
    vConcat (vInsertEnd (vTake v i) e)~vSkip v i
)

~\vReduce(\v\l\d
    vFold (vSkip v 1) (vGet v 0 d) l
)

~\vRemoveAt(\v\i
    vExpand (vIndexify v) (\t
        (iIsEQ (t tFirst) i) [] [~t tSecond]
    )
)